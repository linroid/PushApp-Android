buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt'

/*
 * Copyright (c) linroid 2015.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0
def buildTime = new Date().format("yyyy-MM-dd HH:mm", TimeZone.getTimeZone("GMT+8:00"))

def Properties props
File appPropFile = file('app.properties');
if (appPropFile.exists()) {
    props = new Properties()
    props.load(new FileInputStream(appPropFile))
}

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.linroid.pushapp"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "FIR_API_TOKEN", "\"${props['FIR_API_TOKEN']}\""
        println(new File("./app/nobug.txt").getText("UTF-8"))
        manifestPlaceholders = [JPUSH_CHANNEL: "default", PACKAGE: applicationId]
        if (props) {
            manifestPlaceholders.put("JPUSH_APPKEY", props['JPUSH_APPKEY'] as String)
        }
    }
    signingConfigs {
        debug {
            // No debug config
        }

        release {
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix '-dev'
            shrinkResources false
            minifyEnabled false
            zipAlignEnabled false
            signingConfig signingConfigs.debug
            manifestPlaceholders = [JPUSH_CHANNEL: "default", PACKAGE: defaultConfig.applicationId + applicationIdSuffix]
            if (props) {
                manifestPlaceholders.put("JPUSH_APPKEY", props['DEBUG_JPUSH_APPKEY'] as String)
            }
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        all { type ->
            def host
            if (props['HOST']) {
                host = props['HOST'];
            } else {
                if (type == debug) {
                    def hostIp = InetAddress.getLocalHost().getHostAddress()
                    host = "pushapp.${hostIp}.xip.io"
                    println("You can open the homepage: http://${host}")
                } else if (type == release) {
                    host = "pushapp.linroid.com"
                } else {
                    return
                }
            }
            type.buildConfigField "String", "ENDPOINT", "\"http://${host}/api\""
            type.buildConfigField "String", "HOST", "\"${host}\""
            type.buildConfigField "String", "HOST_URL", "\"http://${host}\""

            type.resValue "string", "endpoint", "http://${host}/api"
            type.resValue "string", "host", "${host}"
            type.resValue "string", "host_url", "http://${host}"
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }


}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.0.0'
    compile 'com.android.support:design:23.0.0'
    compile 'com.android.support:support-v4:23.0.0'
    compile 'com.android.support:cardview-v7:23.0.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.android.support:appcompat-v7:23.0.0'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.journeyapps:zxing-android-embedded:3.0.2@aar'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.mani:thindownloadmanager:1.0.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.sqlbrite:sqlbrite:0.2.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.facebook.stetho:stetho:1.1.1'
    compile 'com.facebook.stetho:stetho-okhttp:1.1.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    //    compile 'com.facebook.fresco:fresco:0.6.0'
    //    compile 'com.facebook.fresco:imagepipeline-okhttp:0.6.0'
    //    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    //    compile 'io.realm:realm-android:0.82.0'
    //    compile "org.parceler:parceler-api:1.0.3"
    //    apt "org.parceler:parceler:1.0.3"
    //    compile 'de.greenrobot:greendao-generator:2.0.0'
    //        compile 'com.github.bunnyblue:ApkAutoInstaller:V1.0.0'
    compile 'org.apache.directory.studio:org.apache.commons.codec:1.8'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true;
    }
}


File signPropFile = file('signing.properties');
if (signPropFile.exists()) {
    def Properties signProps = new Properties()
    signProps.load(new FileInputStream(signPropFile))

    if (signProps.containsKey('STORE_FILE') && signProps.containsKey('STORE_PASSWORD') &&
            signProps.containsKey('KEY_ALIAS') && signProps.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(signProps['STORE_FILE'])
        android.signingConfigs.release.storePassword = signProps['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = signProps['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = signProps['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}